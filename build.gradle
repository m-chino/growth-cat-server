plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '1.1.2'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
idea.module.inheritOutputDirs = true

group = 'jp.gxp'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

// docker imageの設定
// gradlew jib とすることでアプリのビルドからdockerイメージの作成、GCPのcontainer registryへのpushまでやってくれる
// gcpのcontainer registryに上げるためにはgcloudコマンドをインストールした後以下のコマンドを叩く必要がある。
// gcloud auth configure-docker
// 詳しくはここ : https://cloud.google.com/container-registry/docs/advanced-authentication#docker_credential_helper
jib {
    to {
        image = 'asia.gcr.io/acquired-talent-237406/growth-cat-server'
        credHelper = 'gcr'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // https://mvnrepository.com/artifact/org.elasticsearch.client/elasticsearch-rest-high-level-client
    compile('org.elasticsearch.client:elasticsearch-rest-high-level-client:7.2.0') {
        exclude module: 'elasticsearch'
        exclude module: 'elasticsearch-rest-client'
    }
    // https://mvnrepository.com/artifact/org.elasticsearch/elasticsearch
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: '7.2.0'
    // https://mvnrepository.com/artifact/org.elasticsearch.client/elasticsearch-rest-client
    compile group: 'org.elasticsearch.client', name: 'elasticsearch-rest-client', version: '7.2.0'

}

task containerRun(type: Exec) {
    commandLine 'docker'
    args = [
            'run',
            '-d',
            '--rm',
            '--name', 'growth-cat-server',
            '-e', 'ELASTICSEARCH_HOST=localhost',
            '-e', 'ELASTICSEARCH_PORT=9200',
            '-e', 'ELASTICSEARCH_HTTP_PROTOCOL=http',
            '-p', '8080:8080',
            'asia.gcr.io/acquired-talent-237406/growth-cat-server'
    ]
}

task localRun(dependsOn: [jibDockerBuild, containerRun]) {
    doLast {
        logger.info 'growth-cat-server container started'
    }
}

// localRun実行時のタスクの依存関係を設定
containerRun.mustRunAfter jibDockerBuild

task containerStop(type: Exec) {
    commandLine 'docker'
    args = [
            'stop',
            'growth-cat-server'
    ]
    doLast {
        logger.info 'docker container stopped'
    }
}

bootJar {
    baseName = 'growth-cat-server'
    version =  '0.0.1'
}
